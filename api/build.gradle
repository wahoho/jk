group 'com.uzabase.jk'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        kotlin_version = '1.1.50'
        springBootVersion = '2.0.0.M5'
        springVersion = '5.0.0.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-snapshot' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-spring"
apply plugin: "application"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

task buildDart(type: Exec) {
    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.INFO

    workingDir '../front'

    commandLine 'pub', 'build'
}

processResources {
    from ('../front/build/web') {
        into 'public'
    }
}

bootJar.dependsOn buildDart

dependencies {
    // spring
    compile "org.springframework:spring-core:$springVersion"
    compile "org.springframework:spring-webflux:$springVersion"
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-beans:$springVersion"
    compile "org.springframework.data:spring-data-jpa:2.0.0.RELEASE"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"

    compile "io.projectreactor:reactor-core:3.1.0.RELEASE"
    compile "io.projectreactor.ipc:reactor-netty:0.7.0.RELEASE"

    // jackson
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")
    compile("com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr353:$jacksonVersion")

    compile "mysql:mysql-connector-java:5.1.44"
    compile "com.zaxxer:HikariCP:2.7.1"

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    // test
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

mainClassName = "com.forcas.jojo.JojoApplication"

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}